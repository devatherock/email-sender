plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.diffplug.spotless' version '6.5.2'
}

group = 'io.github.devatherock'
version = '0.1.2'

repositories {
    mavenCentral()
}

dependencies {
    def lombokVersion = '1.18.12'
    def spockVersion = '2.3-groovy-2.5'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    compileOnly group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'

    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.26'
    implementation group: 'org.masukomi', name: 'aspirin', version: '0.11.01'
    implementation group: 'org.simplejavamail', name: 'simple-java-mail', version: '5.5.1'
    implementation group: 'org.hibernate.validator', name: 'hibernate-validator', version: '6.1.5.Final'

    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.4.1'

    testImplementation group: 'org.codehaus.groovy', name: 'groovy-json', version: '2.5.22'
    testImplementation group: 'org.spockframework', name: 'spock-core', version: spockVersion
    testImplementation group: 'org.spockframework', name: 'spock-spring', version: spockVersion
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}

bootRun {
    systemProperties System.properties
}

springBoot {
    buildInfo()
}

test {
    useJUnitPlatform()
}

ext.jacoco = [
    exclusions: [
        // TODO Remove once tests are written
        'io/github/devatherock/emailsender/EmailSenderApplication.class',
        'io/github/devatherock/emailsender/config/EmailSenderAppConfig.class',
        'io/github/devatherock/emailsender/config/EmailSenderProperties.class',
        'io/github/devatherock/emailsender/model/EmailSendRequest.class',
        'io/github/devatherock/emailsender/util/YamlHttpMessageConverter.class',
        'io/github/devatherock/emailsender/service/EmailClient.class',
    ],
]

apply from: 'https://raw.githubusercontent.com/devatherock/gradle-includes/master/checks.gradle'