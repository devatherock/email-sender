plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'org.springframework.boot' version '3.0.12'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'com.diffplug.spotless' version '6.22.0'
    id 'org.owasp.dependencycheck' version '8.4.2'
}

group = 'io.github.devatherock'
version = '0.3.0'

repositories {
    mavenCentral()
}

dependencies {
    def lombokVersion = '1.18.28'
    def spockVersion = '2.3-groovy-4.0'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    compileOnly group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'

    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.0'
    implementation group: 'org.masukomi', name: 'aspirin', version: '0.11.01'
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.42.0.0'
    implementation group: 'org.simplejavamail', name: 'simple-java-mail', version: '5.5.1'
    implementation group: 'jakarta.validation', name: 'jakarta.validation-api', version: '3.0.2'

    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.2.0'

    testImplementation group: 'org.apache.groovy', name: 'groovy-json', version: '4.0.11'
    testImplementation group: 'org.spockframework', name: 'spock-core', version: spockVersion
    testImplementation group: 'org.spockframework', name: 'spock-spring', version: spockVersion
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testImplementation group: 'org.subethamail', name: 'subethasmtp', version: '3.1.7'
}

bootRun {
    systemProperties System.properties
}

springBoot {
    buildInfo()
}

// To be able to identify the fat jar in dockerfile with spring boot 2.5+
tasks.named('bootJar') {
    archiveClassifier = 'boot'
}

tasks.withType(Test) {
    useJUnitPlatform()
}

ext.jacoco = [
    exclusions: [
        'io/github/devatherock/emailsender/EmailSenderApplication.class',
        // TODO Remove once tests are written
        'io/github/devatherock/emailsender/config/EmailSenderAppConfig.class',
        'io/github/devatherock/emailsender/config/EmailSenderProperties.class',
        'io/github/devatherock/emailsender/model/EmailSendRequest.class',
        'io/github/devatherock/emailsender/service/EmailClient.class',
    ],
]

apply from: 'https://raw.githubusercontent.com/devatherock/gradle-includes/master/integration.gradle'
apply from: 'https://raw.githubusercontent.com/devatherock/gradle-includes/master/checks.gradle'